# | Register | Usage        | Type    | Description                   |
# | -------- | ------------ | ------- | ----------------------------- |
# | `$a0`    | input        | address | null-terminated input string  |
# | `$a1`    | input/output | address | null-terminated output string |
# | `$t0-9`  | temporary    | any     | used for temporary storage    |

.globl reverse

reverse:
        move    $t0, $a0
        move    $t1, $a1

check_str_end:
        lb      $t2, ($t0)
        beqz    $t2, check_str_start
        addi    $t0, $t0, 1
        j       check_str_end

copy:
        subi    $t0, $t0, 1
        lb      $t2, ($t0)
        sb      $t2, ($t1)
        addi    $t1, $t1, 1

check_str_start:
        bne     $t0, $a0, copy
        sb      $zero, ($t1)
        jr $ra
